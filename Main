Option Explicit
Public SnakeRows() As Integer
Public SnakeColumns() As Integer
Public hInc As Integer
Public vInc As Integer
Dim r As Long
Dim tail As Long
Const Lastrow As Long = 26
Const Lastcolumn As Long = 20
Dim AppleCounter As Long

Sub StartGame()
  
    ReDim SnakeRows(2)
    ReDim SnakeColumns(2)
    
    Sheet1.Range("B2:S25").Interior.Color = vbBlack
        
    SnakeRows(0) = 16 'head
    SnakeRows(1) = 17
    SnakeRows(2) = 18
    
    SnakeColumns(0) = 10
    SnakeColumns(1) = 10
    SnakeColumns(2) = 10
    
    vInc = -1
    hInc = 0
    AppleCounter = 0
    Sheet1.Range("U6").Value = 0
    
    KeyBindings
    addApple
    ShowSnake
    
End Sub

Sub ShowSnake()

    'body
    For r = UBound(SnakeRows) To 1 Step -1
        Cells(SnakeRows(r), SnakeColumns(r)).Interior.Color = vbRed
    Next r
    
    'head
    Cells(SnakeRows(0), SnakeColumns(0)).Interior.Color = vbYellow

End Sub

Sub Movement()
    ' last cell make to black
    If vInc <> 0 Or hInc <> 0 Then
        tail = UBound(SnakeRows)
        Cells(SnakeRows(tail), SnakeColumns(tail)).Interior.Color = vbBlack
        
    'Move body (array made to prev one)
        For r = tail To 1 Step -1
            SnakeRows(r) = SnakeRows(r - 1)
            SnakeColumns(r) = SnakeColumns(r - 1)
        Next r
    
    'moveHead
    
        SnakeRows(0) = SnakeRows(0) + vInc
        SnakeColumns(0) = SnakeColumns(0) + hInc
    End If
    
        If Cells(SnakeRows(0), SnakeColumns(0)).Interior.Color = vbRed Then
            MsgBox "You lost!", vbInformation, "Game over!"
            Exit Sub
        End If
        
        If SnakeRows(0) >= Lastrow Or SnakeColumns(0) >= Lastcolumn Or SnakeColumns(0) < 2 Then
           MsgBox "You lost!", vbInformation, "Game over!"
           Exit Sub
        End If
        
    
    'Got the apple
    If Cells(SnakeRows(0), SnakeColumns(0)).Interior.Color = vbGreen Then
        ReDim Preserve SnakeRows(UBound(SnakeRows) + 1)
        ReDim Preserve SnakeColumns(UBound(SnakeColumns) + 1)
        SnakeRows(UBound(SnakeRows)) = SnakeRows(UBound(SnakeRows) - 1)
        SnakeColumns(UBound(SnakeColumns)) = SnakeColumns(UBound(SnakeColumns) - 1)
        AppleCounter = AppleCounter + 1
        Sheet1.Range("u6").Value = AppleCounter
        addApple
    End If
    
    ShowSnake
End Sub

Sub addApple()
        
    Dim rndRow As Long
    Dim rndColumn As Long
    
    rndRow = Int(rnd * 15) + 2
    rndColumn = Int(rnd * 10) + 2
    
    Cells(rndRow, rndColumn).Interior.Color = vbGreen
End Sub
